Перем _Родитель;
Перем _ПодчиненныеЭлементы;
Перем _Название;
Перем _ЭтоГруппа;
Перем _ВключатьВКонтекстноеМеню;
Перем _Автозамена;
Перем _Текст;

Процедура ПриСозданииОбъекта(Родитель = Неопределено, Название = Неопределено, ЭтоГруппа = Ложь)
	
	УстановитьНазвание(Название);
	УстановитьЭтоГруппа(ЭтоГруппа);
	УстановитьРодителя(Родитель);

	Если ЭтоГруппа() Тогда
		_ПодчиненныеЭлементы = Новый Массив;
	КонецЕсли;
	
	_ВключатьВКонтекстноеМеню = Ложь;
	_Автозамена = "";
	_Текст = "";
	
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Процедура УстановитьРодителя(Родитель) Экспорт
	
	Если _Родитель <> Неопределено Тогда
		Индекс = _Родитель.ПодчиненныеЭлементы().Найти(ЭтотОбъект);
		_Родитель.ПодчиненныеЭлементы().Удалить(Индекс);
	КонецЕсли;

	Если Родитель <> Неопределено Тогда
		Родитель.ПодчиненныеЭлементы().Добавить(ЭтотОбъект);
	КонецЕсли;	
	
	_Родитель = Родитель;
	
КонецПроцедуры

Функция УстановитьНазвание(Название) Экспорт
	
	_Название = Строка(Название);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция УстановитьВключатьВКонтекстноеМеню(ВключатьВКонтекстноеМеню) Экспорт
	
	Если ЭтоГруппа() Тогда
		ВызватьИсключениеСвойствоНедоступноДляГруппы("Включать в контекстное меню");
	КонецЕсли;
	
	_ВключатьВКонтекстноеМеню = Булево(ВключатьВКонтекстноеМеню);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция УстановитьТекстАвтозамены(ТекстАвтозамены) Экспорт
	
	Если ЭтоГруппа() Тогда
		ВызватьИсключениеСвойствоНедоступноДляГруппы("Текст автозамены");
	КонецЕсли;
	
	_Автозамена = Строка(ТекстАвтозамены);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция УстановитьТекстШаблона(ТекстШаблона) Экспорт
	
	Если ЭтоГруппа() Тогда
		ВызватьИсключениеСвойствоНедоступноДляГруппы("Текст шаблона");
	КонецЕсли;
	
	_Текст = Строка(ТекстШаблона);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Название() Экспорт
	
	Возврат _Название;
	
КонецФункции

Функция ЭтоГруппа() Экспорт
	
	Возврат _ЭтоГруппа;
	
КонецФункции

Функция ВключатьВКонтекстноеМеню() Экспорт
	
	Если ЭтоГруппа() Тогда
		ВызватьИсключениеСвойствоНедоступноДляГруппы("Включать в контекстное меню");
	КонецЕсли;
	
	Возврат _ВключатьВКонтекстноеМеню;
	
КонецФункции

Функция ТекстАвтозамены() Экспорт
	
	Если ЭтоГруппа() Тогда
		ВызватьИсключениеСвойствоНедоступноДляГруппы("Текст автозамены");
	КонецЕсли;
	
	Возврат _Автозамена;
	
КонецФункции

Функция ТекстШаблона() Экспорт
	
	Если ЭтоГруппа() Тогда
		ВызватьИсключениеСвойствоНедоступноДляГруппы("Текст шаблона");
	КонецЕсли;
	
	Возврат _Текст;
	
КонецФункции

Функция Родитель() Экспорт

	Возврат _Родитель;

КонецФункции

Функция ПодчиненныеЭлементы() Экспорт
	
	Если Не ЭтоГруппа() Тогда
		ВызватьИсключениеСвойствоНедоступноДляЭлемента("Подчиненные элементы");
	КонецЕсли;

	Возврат _ПодчиненныеЭлементы;
	
КонецФункции

Функция Скопировать() Экспорт

	НовыйЭлемент = Новый ЭлементшаблонаТекста(Родитель(), Название(), ЭтоГруппа());
	
	Если Не ЭтоГруппа() Тогда
		НовыйЭлемент.УстановитьВключатьВКонтекстноеМеню(ВключатьВКонтекстноеМеню());
		НовыйЭлемент.УстановитьТекстАвтозамены(ТекстАвтозамены());
		НовыйЭлемент.УстановитьТекстШаблона(ТекстШаблона());
	КонецЕсли;

	Возврат НовыйЭлемент;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция УстановитьЭтоГруппа(ЭтоГруппа)
	
	_ЭтоГруппа = Булево(ЭтоГруппа);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Процедура ВызватьИсключениеСвойствоНедоступноДляГруппы(ИмяСвойства)
	
	ВызватьИсключение СтрШаблон("Свойство ""%1"" недоступно для группы", ИмяСвойства);
	
КонецПроцедуры

Процедура ВызватьИсключениеСвойствоНедоступноДляЭлемента(ИмяСвойства)
	
	ВызватьИсключение СтрШаблон("Свойство ""%1"" недоступно для элемента", ИмяСвойства);
	
КонецПроцедуры

#КонецОбласти